plugins {
    id 'org.springframework.boot' version '2.7.1'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'java'
    id 'checkstyle'
    id 'jacoco'
}

group = 'com.meoguri'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

ext {
    snippetsDir = file('build/generated-snippets')
}

checkstyle {
    maxWarnings = 0 // 규칙이 어긋나는 코드가 하나라도 있을 경우 빌드 fail을 내고 싶다면 이 선언을 추가한다.
    configFile = file("${rootDir}/checkstyle/naver-checkstyle-rules.xml")
    toolVersion = "10.2"  // checkstyle 버전 8.24 이상 선언
}

jacoco {
    toolVersion = '0.8.7'
}

jacocoTestReport {
    reports {
        html.enabled(true)
        xml.enabled(false)
        csv.enabled(false)
    }

    finalizedBy 'jacocoTestCoverageVerification'

    dependsOn test

    def Qdomains = []
    for (qPattern in "QA".."QZ") {  // qPattern = "*.QA","*.QB","*.QC", ... "*.QZ"
        Qdomains.add("com/meoguri/**/" + qPattern + "*.*")
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "com/meoguri/**/dto/*.*",
                    "com/meoguri/**/util/*.*",
                    "com/meoguri/**/exception/*.*",
                    "com/meoguri/**/*Properties.*",
            ] + Qdomains)
        }))
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled = false
            element = 'BUNDLE'

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }

            excludes = []
        }
    }

    finalizedBy 'asciidoctor'
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-aop'

    // Infrastructure
    implementation 'org.jsoup:jsoup:1.15.2'
    implementation 'com.amazonaws:aws-java-sdk-s3:1.12.272'

    // Database
    implementation 'mysql:mysql-connector-java'
    implementation 'org.flywaydb:flyway-mysql'
    implementation 'com.vladmihalcea:hibernate-types-52:2.17.1'
    implementation "com.querydsl:querydsl-jpa"
    implementation 'com.querydsl:querydsl-sql'

    // Security
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity5'
    implementation 'io.jsonwebtoken:jjwt:0.9.1'


    // Utility
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'com.github.maricn:logback-slack-appender:1.6.1'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    // QueryDSL
    annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jpa"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"

    // Test Support
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'junit' // excluding junit 4
    }

    testImplementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.8.0'
    testImplementation 'com.tngtech.archunit:archunit:0.23.1'
    testImplementation 'io.findify:s3mock_2.12:0.2.6'
}

tasks.named('test') {
    useJUnitPlatform()
    outputs.dir snippetsDir

    finalizedBy 'jacocoTestReport'
}

asciidoctor {
    dependsOn test

    inputs.dir snippetsDir

    sourceDir file('docs/asciidoc')
    sources {
        include 'BOOKMARK-API.adoc'
    }
}

task copyDocument(type: Copy) {
    dependsOn asciidoctor

    from file('build/docs/asciidoc/')
    into file('src/main/resources/static/docs')
}

task copyDev(type: Copy) {
    from file('./config')
    into file('./src/main/resources')
}

build {
    dependsOn copyDev
    dependsOn copyDocument
}
